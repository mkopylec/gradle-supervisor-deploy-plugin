plugins {
    id 'groovy'
    id 'jacoco'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.6

group = "com.github.mkopylec"
archivesBaseName = name
version = "0.1.0"

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '+'
    compile('org.hidetake:groovy-ssh:+') {
        exclude module: 'groovy-all'
    }

    testCompile('org.spockframework:spock-core:+') {
        exclude module: 'groovy-all'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

ext {
    ossrhUsername = project.ext.has('ossrhUsername') ? project.ext.ossrhUsername : ''
    ossrhPassword = project.ext.has('ossrhPassword') ? project.ext.ossrhPassword : ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Gradle Supervisor Deploy Plugin'
                packaging 'jar'
                description ''
                url 'https://github.com/mkopylec/gradle-supervisor-deploy-plugin'

                scm {
                    connection 'scm:git:https://github.com/mkopylec/gradle-supervisor-deploy-plugin.git'
                    developerConnection 'scm:git:https://github.com/mkopylec/gradle-supervisor-deploy-plugin.git'
                    url 'https://github.com/mkopylec/gradle-supervisor-deploy-plugin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mkopylec'
                        name 'Mariusz Kopylec'
                        email 'maniek8888@o2.pl'
                    }
                }
            }
        }
    }
}